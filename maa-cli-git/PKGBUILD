# Maintainer: Horror Proton <107091537+horror-proton@users.noreply.github.com>
pkgname=maa-cli-git
pkgver=v0.3.7.r1.59eb1bc
pkgrel=1
pkgdesc="A simple CLI for MAA by Rust."
arch=('x86_64' 'aarch64')
url="https://github.com/MaaAssistantArknights/maa-cli"
license=('AGPL')
depends=('gcc-libs')
makedepends=('git' 'cargo')
optdepends=('maa-assistant-arknights: for preinstalled maa core')
provides=("${pkgname%-git}")
conflicts=("${pkgname%-git}")
source=("${pkgname%-git}::git+$url.git")
md5sums=('SKIP')

pkgver() {
	cd "$srcdir/${pkgname%-git}"
	printf "%s" "$(git describe --long --tags | sed 's/\([^-]*-\)g/r\1/;s/-/./g')"
}

prepare() {
	cd "$srcdir/${pkgname%-git}"
	export RUSTUP_TOOLCHAIN=stable
	cargo fetch --locked --target "$CARCH-unknown-linux-gnu"
}

build() {
	cd "$srcdir/${pkgname%-git}"
	export RUSTUP_TOOLCHAIN=stable
	export CARGO_TARGET_DIR=target

	export MAA_EXTRA_SHARE_NAME=/usr/share/maa-assistant-arknights
	cargo build --frozen --release --all-features
}

check() {
	cd "$srcdir/${pkgname%-git}"
	export RUSTUP_TOOLCHAIN=stable
	cargo test --frozen --all-features
}

package() {
	cd "$srcdir/${pkgname%-git}"
	install -Dm0755 -t "$pkgdir/usr/bin/" "target/release/maa"
	mkdir -p "$pkgdir/usr/share"
	# ln -s maa-assistant-arknights "$pkgdir/usr/share/maa"

	"$pkgdir"/usr/bin/maa complete bash | install -D /dev/stdin "$pkgdir/usr/share/bash-completion/completions/maa"
	"$pkgdir"/usr/bin/maa complete zsh | install -D /dev/stdin "$pkgdir/usr/share/zsh/site-functions/_maa"
	cp -r "$srcdir/${pkgname%-git}/maa-cli/share" "$pkgdir/usr"
}
