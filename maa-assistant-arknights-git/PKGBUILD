# Maintainer: Horror Proton <107091537+horror-proton@users.noreply.github.com>

pkgname=maa-assistant-arknights-git
pkgver=4.13.0.r20.967149abc
pkgrel=1
pkgdesc="An Arknights assistant"
arch=(x86_64)
url="https://github.com/MaaAssistantArknights/MaaAssistantArknights"
license=('AGPL')
depends=(opencv onnxruntime cpr)
makedepends=(asio eigen git cmake)
provides=("${pkgname%-git}")
conflicts=("${pkgname%-git}")
source=("MaaAssistantArknights::git+$url.git#branch=dev"
        'FastDeploy::git+https://github.com/MaaAssistantArknights/FastDeploy.git')
md5sums=('SKIP'
         'SKIP')

pkgver() {
    cd "$srcdir/MaaAssistantArknights"
    printf "%s" "$(git describe --long --tags --match='v*' | rev | sed 's/-//3' | rev | sed 's/^v//;s/\([^-]*-\)g/r\1/;s/-/./g')"
}

prepare() {
    cd "$srcdir/MaaAssistantArknights"
    sed -i 's/RUNTIME\sDESTINATION\s\./ /g; s/LIBRARY\sDESTINATION\s\./ /g; s/PUBLIC_HEADER\sDESTINATION\s\./ /g' CMakeLists.txt
    sed -i 's/find_package(asio /# find_package(asio /g' CMakeLists.txt
    sed -i 's/asio::asio/ /g' CMakeLists.txt
}

build() {
    cd "$srcdir"
    cmake -B build-fastdeploy -S FastDeploy \
        -DCMAKE_CXX_FLAGS=-fPIC\ -fmacro-prefix-map="$srcdir"= \
        -DCMAKE_BUILD_TYPE=None \
        -DBUILD_SHARED_LIBS=ON \
        -DCMAKE_INSTALL_PREFIX="$srcdir"/installed/usr
    cmake --build build-fastdeploy

    mkdir -p installed/usr
    cmake --install build-fastdeploy --prefix "$srcdir"/installed/usr

    cd "$srcdir"
    cmake -B build -S MaaAssistantArknights \
        -DCMAKE_BUILD_TYPE=None \
        -DCMAKE_PREFIX_PATH="$srcdir"/installed/usr \
        -DCMAKE_CXX_FLAGS=-isystem\ "$srcdir"/installed/usr/include\ -fmacro-prefix-map="$srcdir"= \
        -DUSE_MAADEPS=OFF \
        -DINSTALL_THIRD_LIBS=ON \
        -DINSTALL_RESOURCE=ON \
        -DINSTALL_PYTHON=ON \
        -DMAA_VERSION=v"$pkgver"
    cmake --build build
}

package() {
    cd "$srcdir"
    mkdir -p "$pkgdir"/usr
    mv -fv "$srcdir"/installed/usr/lib "$pkgdir"/usr
    cmake --install build --prefix "$pkgdir"/usr

    cd "$pkgdir"/usr
    mkdir -p share/"${pkgname%-git}"
    mv Python share/"${pkgname%-git}"
    mv resource share/"${pkgname%-git}"
}
